'
Dim Regler%(10,6)
Dim Reglern$(10)
'
' #########################################
' #           Deko-Simulation             #
' #########################################
'
'
Dim Gewebe(16,2)         ! halbwertszeit,  a,  b,
For X%=1 To 16
  Read Gewebe(X%,0)
  Read Gewebe(X%,1)
  Read Gewebe(X%,2)
Next X%
'
Dim Inertgdrgew(16)      ! inertgasdruck in den kompartimenten
'
Dim Inertgdrtol(16,2)    ! tolerieter umgeb.druck,  dekostufe, dekozeit
'
@Init
'
'       Hauptprogramm
'
Repeat
  If Timer-Timestate%>Seclength% Then   ! zeitberechnung
    If Tiefe%>0
      Inc Sec%
      Timestate%=Timer
    Else
      Timestate%=Timer
    Endif
  Endif
  If Sec%>=60 Then
    Inc Min%
    Sec%=Sec%-60
    Nmin!=True
  Endif
  '
  Tiefe%=Int(Regler%(0,5))
  Hoehe%=Int(Regler%(1,5))
  '
  If Althoehe%<>Hoehe%
    Althoehe%=Hoehe%
    @Hoehenbereich
    Graphmode 1
    Text 10,70,"BEREICH   : "+Str$(Ber%)+"  "
    Text 10,50,"HôHE      : "+Str$(Hoehe%)+" m.Å.M  "
  Endif
  If Alttiefe%<>Tiefe%
    Graphmode 1
    Text 10,130,"TIEFE    : "+Str$(Tiefe%)+" m "
    Alttiefe%=Tiefe%
  Endif
  '
  Umgebdruck=(Tiefe%)/10+Atmkomp          ! in bar
  Inertgdrinsp=Int(100*(Umgebdruck-0.063)*Inertgasanteil)/100
  '
  If Nmin!               ! nur alle minute anzeigen
    Nmin!=False
    '
    Line Altx,Alty,320+Min%*2,40+Tiefe%*2   ! Grafik
    Altx=321+Min%*2
    Alty=40+Tiefe%*2
    '
    For X%=1 To 16      ! Gewebespannungen,erlaubte Umgebungsdruecke
      K=0.69315/Gewebe(X%,0)
      Inertgdrgew(X%)=Inertgdrgew(X%)+(Inertgdrinsp-Inertgdrgew(X%))*(1-Exp(-K))
      Inertgdrtol(X%,0)=(Inertgdrgew(X%)-Gewebe(X%,1))*Gewebe(X%,2)
      If Inertgdrtol(X%,0)=0
        Inertgdrtol(X%,0)=0
      Endif
    Next X%
    '
    Graphmode 1
    Text 10,110,"ZEIT     : "+Str$(Min%)+"' "
    Text 10,90,"NULLZEIT : "+Nullzeit$+"'    "
    Text 10,150,"UMGEBUNGSDRUCK  [bar] : "+Str$(Umgebdruck)
    Text 10,170,"INERTG.DRUCK (insp.)  : "+Str$(Inertgdrinsp)
    Text 10,195,"INERTG.DRUCK (gewebe) : "
    Text 10,270,"ERLAUBTER UMGEB.DRUCK : "
    For X%=1 To 16                               !n2-druecke,tolerierte dr. anzeigen
      Text 10+(X%-1)*40,220,Str$(X%)
      Text 5+(X%-1)*40,245,Str$(Int(Inertgdrgew(X%)*100)/100)
      Text 5+(X%-1)*40,290,Str$(Int(Inertgdrtol(X%,0)*100)/100)+" "
    Next X%
    '
    Text 10,310,"DEKOSTUFE/DEKOZEIT/RESTNULLZEIT:" ! dekostufen berechnen
    '
    For X%=1 To 16
      Stufe=Int((Inertgdrtol(X%,0)-Atmdekomp+0.3)/0.3)*3
      Inertgdrtol(X%,1)=Stufe
      If Stufe<=0
        Stufe$="-  "
      Else
        Stufe$=Str$(Stufe)+" "
      Endif
      '
      If Stufe>0              ! dekozeit berechnen
        K=0.69315/Gewebe(X%,0)
        A=Gewebe(X%,1)
        B=Gewebe(X%,2)
        Dekodruck=Atmdekomp+0.1*(Stufe-3)   !umgebungsdruck nÑchsthîhere dekostufe
        Umgebdr=Atmkomp+0.1*Stufe
        Gdrinsp=(Umgebdr-0.063)*Inertgasanteil
        Pnull=Inertgdrgew(X%)
        If 1+(Pnull-A-Dekodruck/B)/(Gdrinsp-Pnull)>0
          Inertgdrtol(X%,2)=Log(1+(Pnull-A-Dekodruck/B)/(Gdrinsp-Pnull))/(-K)
        Endif
      Else
        Inertgdrtol(X%,2)=0
      Endif
      If Inertgdrtol(X%,2)<>0
        Zeit$=Str$(Int(Inertgdrtol(X%,2)*10)/10)
      Else
        Zeit$="-    "
      Endif
      Text 5+(X%-1)*40,330,Stufe$
      Text 5+(X%-1)*40,350,Zeit$
    Next X%
    '
    '
    Tiefste=0
    Laengste=0
    For X%=1 To 16                   !tiefste dekostufe suchen
      If Inertgdrtol(X%,1)>Tiefste
        Tiefste=Inertgdrtol(X%,1)
      Endif
    Next X%
    For X%=1 To 16                   !lÑngste mit obiger tiefe
      If Inertgdrtol(X%,2)>Laengste And Inertgdrtol(X%,1)=Tiefste
        Laengste=Inertgdrtol(X%,2)
      Endif
    Next X%
    '
    If Tiefste>0
      Text 10,390,"--> Unterste Dekostufe :"
      Text 250,390,Str$(Tiefste)+" m  "+Str$(Int(Laengste+0.99))+"'    "
    Endif
    '
    '                       restnullzeit berechnen
    Nullzeit=1000
    X%=0
    Repeat
      Inc X%
      K=0.69315/Gewebe(X%,0)
      A=Gewebe(X%,1)
      B=Gewebe(X%,2)
      Pnull=Inertgdrgew(X%)
      Dekodruck=Atmdekomp           ! atmosphaerendruck
      '
      Pmax=Dekodruck/B+A
      '
      If (Inertgdrtol(X%,1)>0) !  schon deko -> keine Nullzeit
        Nullzeitx=0
      Else
        If Inertgdrinsp>Pmax And Pnull<Pmax
          Nullzeitx=Log(1+(Pnull-A-Dekodruck/B)/(Inertgdrinsp-Pnull))/(-K)
        Else
          Nullzeitx=1000          ! i<pmax,pnull<pmax --> unendlich
        Endif
      Endif
      '
      If Nullzeitx<Nullzeit  ! kÅrzeste NZ ist gÅltig
        Nullzeit=Nullzeitx
      Endif
      '
      Nullzeitx$=Str$(Int(Nullzeitx))
      If Nullzeitx>=1000
        Nullzeitx$=Chr$(223)
      Endif
      Text 5+(X%-1)*40,370,Nullzeitx$+"   "
    Until X%=16
    '
    Nullzeit=Int(Nullzeit)     ! runden
    Nullzeit$=Str$(Nullzeit)
    If Nullzeit>=1000
      Nullzeit$=Chr$(223)
    Endif
  Endif
  '
  @Asksliders
  If Mousek=2
    @Init
  Endif
  '
Until Mousek=3
'
End
'
'
'
Procedure Hoehenbereich
  If Hoehe%<3500
    Atmkomp=0.74
    Atmdekomp=0.67
    Ber%=4
  Endif
  If Hoehe%<=2500
    Atmkomp=0.84
    Atmdekomp=0.76
    Ber%=3
  Endif
  If Hoehe%<=1500
    Atmkomp=0.93
    Atmdekomp=0.86
    Ber%=2
  Endif
  If Hoehe%<=700
    Atmkomp=1.03
    Atmdekomp=0.95
    Ber%=1
  Endif
Return
'
'
Procedure Init
  @Hoehenbereich
  Cls
  Print
  Print "          Dekompressions-simulation     M.Lardelli"
  '
  Reglerpointer%=0
  Gosub Initschieberegler(280,40,150,30,"DEPTH",0,65)
  Gosub Initschieberegler(240,40,150,30,"HôHE",0,4000)
  '
  Timestate%=Timer
  '
  Sec%=0
  Min%=0
  Tiefe%=0
  Alttiefe%=0
  Althoehe%=-1
  Nullzeit$="0"
  Nullzeit=0
  '
  Inertgasanteil=0.79
  For X%=1 To 16
    Inertgdrgew(X%)=0.75
  Next X%
  '
  For X%=1 To 16
    Inertgdrtol(X%,0)=0
    Inertgdrtol(X%,1)=0
    Inertgdrtol(X%,2)=0
  Next X%
  '
  Seclength%=3       ! normal: 200
  Nmin!=False
  '
  Line 320,190,600,190
  Line 320,190,320,40
  Defline 6
  Line 320,40,600,40
  Defline 1
  Line 600,190,600,40
  Altx=320
  Alty=40
Return
'
'
' ******************  SLIDERS  ************************************************
'
Procedure Initschieberegler(X,Y,Lng,Br,Name$,Def,Skala)
  Regler%(Reglerpointer%,1)=X
  Regler%(Reglerpointer%,2)=Y
  Regler%(Reglerpointer%,3)=Lng
  Regler%(Reglerpointer%,4)=Br
  Regler%(Reglerpointer%,5)=Def
  Regler%(Reglerpointer%,6)=Skala
  Reglern$(Reglerpointer%)=Name$
  Inc Reglerpointer%
  '
  Box X,Y,X+Br,Y+Lng
  Gosub Printslider(Reglerpointer%-1)
Return
'
Procedure Printslider(Number)
  Lng=Regler%(Number,3)
  Lng2=Lng-Lng/10
  Pos=Regler%(Number,5)*Lng2/Regler%(Number,6)
  Y1=Pos-Lng/20+Regler%(Number,2)+Lng/20+1
  Y2=Pos+Lng/20+Regler%(Number,2)+Lng/20-1
  Box Regler%(Number,1)+2,Y1,Regler%(Number,1)-2+Regler%(Number,4),Y2
Return
'
Procedure Asksliders
  Mouse X,Y,K
  If K=1
    Sel%=-1
    For I%=0 To Reglerpointer%
      If X>Regler%(I%,1) And X<Regler%(I%,1)+Regler%(I%,4) And Y>Regler%(I%,2)+Regler%(I%,3)/20 And Y<Regler%(I%,2)+Regler%(I%,3)/20*19
        Sel%=I%
      Endif
    Next I%
    If Sel%>-1
      Graphmode 3
      Gosub Printslider(Sel%)
      Qdef=(Y-Regler%(Sel%,3)/20-Regler%(Sel%,2))*Regler%(Sel%,6)/(Regler%(Sel%,3)/10*9)
      Regler%(Sel%,5)=Qdef
      Gosub Printslider(Sel%)
      Event%=Sel%
    Else
      Event%=-1
    Endif
  Else
    Event%=-1
  Endif
Return
'
' daten der 16 kompartimente: halbwertszeit, a,b
'
Data 2.65,  2.2,  0.820
Data 7.94,  1.5,  0.820
Data 12.2,  1.05, 0.825
Data 18.5,  0.9,  0.835
Data 26.5,  0.75, 0.845
Data 37,    0.6,  0.860
Data 53,    0.45, 0.870
Data 79,    0.43, 0.890
Data 114,   0.43, 0.890
Data 146,   0.43, 0.931
Data 185,   0.43, 0.931
Data 238,   0.35, 0.943
Data 304,   0.23, 0.962
Data 397,   0.23, 0.962
Data 503,   0.23, 0.962
Data 635,   0.23, 0.962
