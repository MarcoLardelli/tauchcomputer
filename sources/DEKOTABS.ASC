'
'
' #########################################
' #           Deko-tabellenberechnung     #
' #########################################
'
' DEKOTABS.BAS
'
'
Maxtiefenstufe%=10
Maxzeit%=60
Maxdeko%=2         !nur bis deko 6/4m tabelle berechnen
'
Buehlmann=False
Hoehe%=0
'
If Buehlmann
  If Hoehe%>700
    Dekostufe%=2            ! 12/9/6/3 oder 8/6/4/2
  Else
    Dekostufe%=3
  Endif
  Amerikanisch=True       !tiefenstufen in fuss(12/15/18/21) oder Meter(10/15/20)
  Maxoxygen=False         !immer grîsstmîglicher o2-anteil im atemgas
  Dekooxygen=False        ! ab 6m dekompression mit reinem o2
  Hoehenstufen=True       !verwenden von Hoehenstufe oder exakte berechnung
  Sicherheitszuschlag%=2
Else
  Dekostufe%=3            ! 12/9/6/3 oder 9/6/4/2
  Amerikanisch=True       !tiefenstufen in fuss(12/15/18/21) oder Meter(10/15/20)
  Maxoxygen=False         !immer grîsstmîglicher o2-anteil im atemgas
  Dekooxygen=False        ! ab 6m dekompression mit reinem o2
  Hoehenstufen=False       !verwenden von Hoehenstufe oder exakte berechnung
  Sicherheitszuschlag%=2
Endif
'
'
'
'
'
'
Dim Gewebe(16,2)         ! halbwertszeit,  a,  b,
For X%=1 To 16
  Read Gewebe(X%,0)
  Read Gewebe(X%,1)
  Read Gewebe(X%,2)
Next X%
'
Dim N2inertgdrgew(16)      ! inertgasdruck in den kompartimenten
Dim Heinertgdrgew(16)
'
Dim Inertgdrtol(16,2)    ! tolerieter umgeb.druck,  dekostufe, dekozeit
'
Dim Stufen%(8)           ! 1-8 = stufen 3m - 24m
'
Dim Tiefe(20)
If Amerikanisch
  Tiefe(1)=9
  Tiefe(2)=12
  Tiefe(3)=15
  Tiefe(4)=18
  Tiefe(5)=21
  Tiefe(6)=24
  Tiefe(7)=27
  Tiefe(8)=30
  Tiefe(9)=33
  Tiefe(10)=36
  Tiefe(11)=39
  Tiefe(12)=42
  Tiefe(13)=45
  Tiefe(14)=48
  Tiefe(15)=51
Else
  Tiefe(1)=10
  Tiefe(2)=15
  Tiefe(3)=20
  Tiefe(4)=25
  Tiefe(5)=30
  Tiefe(6)=35
  Tiefe(7)=40
  Tiefe(8)=45
  Tiefe(9)=50
  Tiefe(10)=55
  Tiefe(11)=60
  Tiefe(12)=65
  Tiefe(13)=70
  Tiefe(14)=75
  Tiefe(15)=80
Endif
'
'
@Init
'
Print
Print "Hoehe : ";Str$(Hoehe%);" m.Å.M       Bereich : ";Str$(Ber%)
Print
Input "Drucker/Bildschirm (D/B)   :",Dev$
Dev$=Upper$(Dev$)
'
If Dev$="D"
  Lprint
  Lprint "Hoehe : ";Str$(Hoehe%);" m.Å.M      Bereich : ";Str$(Ber%)
  If Maxoxygen
    Lprint "Tauchen mit maximalem O2-Anteil"
  Endif
  If Dekooxygen
    Lprint "Dekompression ab 6m mit reinem O2"
  Endif
  Lprint "Sicherheitszuschlag zur Tiefe : ";Sicherheitszuschlag%;"m"
  Lprint
  Lprint
  Lprint " Tiefe   Zeit      ";
  For X%=8 Downto 1
    Lprint Using "###    ",X%*Dekostufe%;
  Next X%
  Lprint
  Lprint
Endif
'
For O%=1 To Maxtiefenstufe%
  T%=Tiefe(O%)
  Cls
  Print " Tiefe: ";T%
  Print " Tiefe   Zeit      ";
  For X%=8 Downto 1
    Print Using "###    ",X%*Dekostufe%;
  Next X%
  Print
  Print
  '
  '
  T%=T%+Sicherheitszuschlag%
  @Nullzeitberechnen(T%)
  Print " Nullzeit : ";Nullzeit
  Print
  If Dev$="D"
    Lprint
    Lprint " ";T%;" m    Nullzeit = ";Nullzeit;" '"
  Endif
  '
  For Z%=5 To Maxzeit% Step 5
    @Tauchgang(T%,Z%)
  Next Z%
  '
  Repeat
  Until Inkey$<>""
Next O%
End
'
'
Procedure Nullzeitberechnen(Tiefe%)
  Local X%,A,B,Stufe,K,Dekodruck,Umgebdruck,Inertgdrinsp,Pnull
  X%=0
  @Tauchganginit
  Nullzeit=1000
  Repeat
    Inc X%
    K=0.69315/Gewebe(X%,0)
    A=Gewebe(X%,1)
    B=Gewebe(X%,2)
    Pnull=N2inertgdrgew(X%)
    Dekodruck=Atmdekomp
    '
    Umgebdruck=(Tiefe%)/10+Atmkomp          ! in bar
    If Maxoxygen
      Inertgasanteil=1-1.6/Umgebdruck
    Else
      Inertgasanteil=0.79
    Endif
    If Inertgasanteil<0
      Inertgasanteil=0
    Endif
    Inertgdrinsp=(Umgebdruck-0.063)*Inertgasanteil
    '
    Pmax=Dekodruck/B+A
    '
    If (Inertgdrtol(X%,1)>0)
      Nullzeitx=0
    Else
      If Inertgdrinsp>Pmax And Pnull<Pmax
        Nullzeitx=Log(1+(Pnull-A-Dekodruck/B)/(Inertgdrinsp-Pnull))/(-K)
      Else
        Nullzeitx=1000
      Endif
    Endif
    '
    If Nullzeitx<Nullzeit
      Nullzeit=Nullzeitx
    Endif
  Until X%=16
  '
  Nullzeit=Int(Nullzeit-0.5)
Return
'
'
Procedure Tauchgang(Maxtiefe%,Zeit%)
  Local M%,Z%
  M%=Maxtiefe%
  Z%=Zeit%
  @Tauchganginit
  Drucken=False
  Repeat
    @Tauchen(Maxtiefe%,Zeit%)
    '    @Auftauchen(Maxtiefe%,Tiefste)   !zum nÑchsten dekostop
    If Not Nodeko
      Stufen%(Tiefste/Dekostufe%)=Int(Laengste+1)
      Drucken=True
    Endif
    Maxtiefe%=Tiefste
    Zeit%=Int(Laengste+1)
  Until Nodeko
  If Drucken
    Print Using "### m   ",M%-Sicherheitszuschlag%;
    Print Using "### '      ",Z%;
    If Dev$="D"
      Lprint Using "### m   ",M%;
      Lprint Using "### '      ",Z%;
    Endif
    @Stufenprinten            !ausdrucken der dekostufen/zeiten
    Print
    If Dev$="D"
      Lprint
    Endif
  Endif
Return
'
'
Procedure Stufenprinten
  Local X%
  For X%=8 Downto 1
    If Stufen%(X%)>0
      Print Using "###    ",Stufen%(X%);
      If Dev$="D"
        Lprint Using "###    ",Stufen%(X%);
      Endif
    Else
      Print "  -    ";
      If Dev$="D"
        Lprint "  -    ";
      Endif
    Endif
  Next X%
Return
'
'
Procedure Auftauchen(Von,Bis)  ! entsÑttigung wÑhrend des auftauchens berÅcks.
  Local Diff,Tiefe
  Diff=Von-Bis
  Auftauchzeit=Diff/10                    ! 10m/min
  For Tiefe=Von+Diff/10 To Bis-Diff/10 Step Diff/10     ! in 10 schritten berechnen
    @Tauchen(Tiefe,Auftauchzeit/8)
  Next Tiefe
Return
'
'
Procedure Tauchen(Tiefe%,Zei%)
  Local Min%,Umgebdruck
  Umgebdruck=(Tiefe%)/10+Atmkomp          ! in bar
  If Maxoxygen
    Inertgasanteil=1-1.6/Umgebdruck
  Else
    Inertgasanteil=0.79
  Endif
  If Tiefe%=<6 And Dekooxygen
    Inertgasanteil=0
  Endif
  If Inertgasanteil<0
    Inertgasanteil=0
  Endif
  Inertgdrinsp=(Umgebdruck-0.063)*Inertgasanteil
  @Gewebeberechnen(Zei%)
  @Dekostufen
  @Tiefste
Return
'
'
Procedure Gewebeberechnen(Zei%)
  Local K,X%
  For X%=1 To 16
    K=0.69315/Gewebe(X%,0)
    N2inertgdrgew(X%)=N2inertgdrgew(X%)+(Inertgdrinsp-N2inertgdrgew(X%))*(1-Exp(-K*Zei%))
    Inertgdrtol(X%,0)=(N2inertgdrgew(X%)-Gewebe(X%,1))*Gewebe(X%,2)
  Next X%
Return
'
'
Procedure Dekostufen
  Local X%,A,B,Stufe,K,Dekodruck,Umgebdr,Gdrinsp,Pnull
  For X%=1 To 16
    Stufe=Int((Inertgdrtol(X%,0)-Atmdekomp+Dekostufe%/10)/Dekostufe%*10)*Dekostufe%
    Inertgdrtol(X%,1)=Stufe
    '
    If Stufe>0              ! dekozeit berechnen
      K=0.69315/Gewebe(X%,0)
      A=Gewebe(X%,1)
      B=Gewebe(X%,2)
      Dekodruck=Atmdekomp+0.1*(Stufe-Dekostufe%)           !umgebungsdruck nÑchsthîhere dekostufe
      Umgebdr=Atmkomp+0.1*Stufe
      If Maxoxygen
        Inertganteil=1-1.6/Umgebdr
      Else
        Inertganteil=0.79
      Endif
      If Stufe<9 And Dekooxygen
        Inertganteil=0.1    !dekompression mit reinem sauerstoff ab 6m
      Endif
      If Inertganteil<0
        Inertganteil=0
      Endif
      Gdrinsp=(Umgebdr-0.063)*Inertganteil
      Pnull=N2inertgdrgew(X%)
      If 1+(Pnull-A-Dekodruck/B)/(Gdrinsp-Pnull)>0
        Inertgdrtol(X%,2)=Log(1+(Pnull-A-Dekodruck/B)/(Gdrinsp-Pnull))/(-K)
      Endif
    Else
      Inertgdrtol(X%,2)=0
    Endif
  Next X%
Return
'
'
Procedure Tiefste
  Local X%
  Tiefste=0
  Laengste=-1
  For X%=1 To 16                   !tiefste
    If Inertgdrtol(X%,1)>Tiefste
      Tiefste=Inertgdrtol(X%,1)
    Endif
  Next X%
  For X%=1 To 16                   !lÑngste mit obiger tiefe
    If Inertgdrtol(X%,2)>Laengste And Inertgdrtol(X%,1)=Tiefste
      Laengste=Inertgdrtol(X%,2)
    Endif
  Next X%
  '
  If Tiefste=0
    Nodeko=True
  Else
    Nodeko=False
  Endif
Return
'
'
'
Procedure Hoehenbereich
  If Hoehe%<3500
    Atmkomp=0.74
    Atmdekomp=0.67
    Ber%=4
  Endif
  If Hoehe%<=2500
    Atmkomp=0.84
    Atmdekomp=0.76
    Ber%=3
  Endif
  If Hoehe%<=1500
    Atmkomp=0.93
    Atmdekomp=0.86
    Ber%=2
  Endif
  If Hoehe%<=700
    Atmkomp=1.03
    Atmdekomp=0.95
    Ber%=1
  Endif
  If Not Hoehenstufe
    Atmkomp=1
    Atmdekomp=0.95
  Endif
Return
'
'
'
Procedure Tauchganginit
  Local X%
  '
  For X%=1 To 8
    Stufen%(X%)=0
  Next X%
  '
  For X%=1 To 16
    N2inertgdrgew(X%)=0.75
  Next X%
  '
  For X%=1 To 16
    Inertgdrtol(X%,0)=0
    Inertgdrtol(X%,1)=0
    Inertgdrtol(X%,2)=0
  Next X%
Return
'
'
Procedure Init
  @Hoehenbereich
  Cls
  Print
  Print "          Deko-tabellen Berechnung     M.Lardelli"
  '
  Inertgasanteil=0.79
Return
'
'
'
'
' daten der 16 kompartimente: halbwertszeit, a,b
'
Data 2.65,  2.2,  0.820
Data 7.94,  1.5,  0.820
Data 12.2,  1.05, 0.825
Data 18.5,  0.9,  0.835
Data 26.5,  0.75, 0.845
Data 37,    0.6,  0.860
Data 53,    0.45, 0.870
Data 79,    0.43, 0.890
Data 114,   0.43, 0.890
Data 146,   0.43, 0.931
Data 185,   0.43, 0.931
Data 238,   0.35, 0.943
Data 304,   0.23, 0.962
Data 397,   0.23, 0.962
Data 503,   0.23, 0.962
Data 635,   0.23, 0.962
